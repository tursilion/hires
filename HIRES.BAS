
10 DIM CC$(126),HPAT$(4)
11 FOR A=0 TO 126
12 CC$(A)="0000000000000000"
13 NEXT A
20 CURCHAR=33
25 STARTCHAR=33
30 HEX$="0123456789ABCDEF"
32 HPAT$(0)="89ABCDEF89ABCDEF"
34 HPAT$(1)="45674567CDEFCDEF"
36 HPAT$(2)="23236767ABABEFEF"
38 HPAT$(3)="1133557799BBDDFF"
50 GOTO 500
100 REM  *PLOT*  TI BASIC 
110 REM  HI-RES SUBROUTINE 
120 MR=INT(DOTROW/8)+1
130 MC=INT(DOTCOL/8)+1
140 CALL GCHAR(MR,MC,CH)
150 IF CH>=STARTCHAR THEN 160
155 CH=CURCHAR
157 CURCHAR=CURCHAR+1
160 TC$=CC$(CH-STARTCHAR)
170 XR=DOTROW-(MR-1)*8
180 XC=DOTCOL-(MC-1)*8
190 P=XR*2+1
210 IF XC<4 THEN 220
212 P=P+1
214 XC=XC-4
220 X$=SEG$(TC$,P,1)
260 TT$=SEG$(HPAT$(XC),POS(HEX$,X$,1),1)
270 TC$=SEG$(TC$,1,P-1)&TT$&SEG$(TC$,P+1,16-P)
280 CALL CHAR(CH,TC$)
290 CC$(CH-STARTCHAR)=TC$
295 CALL HCHAR(MR,MC,CH)
300 RETURN
500 REM  SINS 
520 FOR A=1 TO 16
530 CALL COLOR(A,16,2)
540 NEXT A
560 CALL CLEAR
570 FOR CNT=46 TO 23 STEP -23
580 FOR DOTCOL=0 TO 255
590 DOTROW=INT(SIN(6.283*DOTCOL/255)*CNT+CNT*2+.5)
600 GOSUB 100
610 NEXT DOTCOL
620 NEXT CNT
930 GOTO 930
